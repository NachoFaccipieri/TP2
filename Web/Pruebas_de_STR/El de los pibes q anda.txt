;; Variables globales inicializadas por defecto en cero
globals [
  pasajeros_plataforma_superior_tren
  pasajeros_plataforma_superior_colectivo
  pasajeros_plataforma_inferior_subte
  pasajeros_plataforma_inferior_colectivo
  hora_actual
  glob_actual
]

;; -------------------- DEFINICIÓN DE AGENTE PARCELA ---------------------------------

;; Cada patch tiene su:
;; elevación: Es 0 para las calles o vías y 1 para las plataformas de espera de pasajeros
;; plataforma: Determina si está en una plataforma y en que número de plataforma
;; lugar_espera: Determina si un lugar de espera es de colectivo (1) o no (2)
;; libre?: Determina si el lugar está libre o hay algún pasajero ocupando ese lugar
patches-own [ elevacion plataforma lugar_espera libre? ]


;; -------------------- DEFINICIÓN DE AGENTES MÓVILES -----------------------------

;; Tren
;;  estado 0: Lejano (El tren es invisible a la simulación)
;;  estado 1: Llegando (El tren se encuentra arrivando a la estación)
;;  estado 2: Detenido (El tren se encuentra detenido en el andén)
;;  estado 3: Partiendo (El tren parte de la estación)
trenes-own [ estado espera_t t_detenido_t ]

;; Subte
;;  estado 0: Lejano (El subte es invisible a la simulación)
;;  estado 1: Llegando (El subte se encuentra arrivando a la estación)
;;  estado 2: Detenido (El subte se encuentra detenido en el andén)
;;  estado 3: Partiendo (El subte parte de la estación)
subtes-own [ estado espera_s t_detenido_s ]

;; Colectivos
;;  estado 0: Lejano (El colectivo es invisible a la simulación)
;;  estado 1: En marcha (El colectivo está arrivando o partiendo de la parada)
;;  estado 2: Detenido (El colectivo se encuentra detenido en la parada)
colectivos-own [ estado espera_c t_detenido_c ]

;; Pasajeros
;;  estado 10: Caminar hasta la parada (se dirige hasta su lugar elegido de espera)
;;  estado 11: Esperar transporte (se dirige y espera en el lugar elegido)
;;  estado 12: Subir al transporte (se dirige hacia el transporte si está detenido)
;;  estado 13: Caminar hasta senda peatonal (se dirige hacia el comienzo de la senda para cruzar la calle)
;;  estado 14: Cruzar la senda (cruza la senda en sentido a la plataforma destino)
;;  estado 15: Caminar a la salida (se dirige hacia la salida de la plataforma)
pasajeros-own [ estado plataforma_destino edad calle_plataforma lugar_elegido transporte_elegido tiempo_espera]

;; Plurales
breed [trenes tren]
breed [subtes subte]
breed [colectivos colectivo]
breed [pasajeros pasajero]


;; -------------------------- SETUP ESTACIONARIO ---------------------------

to setup-patches
  set-patch-size 15
  ask patches with [ abs(pycor) < 6 ] [set elevacion 0]
  ask patches with [ abs(pycor) > 6 and abs(pycor) < 15] [set elevacion 1]
  ask patches with [ abs(pycor) > 15 and abs(pycor) <= 20 ] [set elevacion 0]
  ask patches with [ elevacion = 1 and pycor > 0 ] [set plataforma 0]
  ask patches with [ elevacion = 1 and pycor < 0 ] [set plataforma 1]

  ;; Definir lugares de espera
  let y_posibles_colectivo (list 9 10 11 12)
  let y_posibles_tren_subte (list 11 12 13 14)

  ;; Los lugares de espera 1 son lugares de espera de colectivos
  ask patches with [ elevacion = 1 and abs(pycor) > 7 ] [
    if (member? abs(pycor) y_posibles_colectivo ) [
      if (pxcor >= -10 and pxcor <= 3 and pxcor mod 2 = 1) [
        set lugar_espera 1
        set libre? true
      ]
    ]
  ]

  ;; Los lugares de espera 2 son lugares de espera de tren y subte, esperan en lugares impares para dejar lugar a bajar
  ask patches with [ elevacion = 1 and abs(pycor) < 14 ] [
    if (member? abs(pycor) y_posibles_tren_subte ) [
      if (pxcor >= 6 and pxcor <= 16 and pxcor mod 2 = 1) [
        set lugar_espera 2
        set libre? true
      ]
    ]
  ]

  ;; Pintar según elevación
  ask patches with [ elevacion = 0 ] [set pcolor black + random(3)]
  ask patches with [ elevacion = 1 ] [set pcolor 5 + random(4)]

  ;; Pintar líneas
  ask patches with [ elevacion = 0 and abs(pycor) = 0] [
    ifelse (pxcor mod 3 = 0) [
        set pcolor black + random(3)
    ] [set pcolor yellow]
  ]
  ask patches with [abs(pycor) = 6 ] [set pcolor yellow]
  ask patches with [abs(pycor) = 15 ] [set pcolor yellow]

  ;; Pintar senda peatonal
  ask patches with [ elevacion = 0 and pxcor >= 10 and pxcor <= 14 and pycor mod 2 = 0 and abs(pycor) < 6 ] [set pcolor white]


  ;; Pintar vias y durmientes
  ask patches with [ elevacion = 0 and abs(pycor) >= 16] [
    if (pxcor mod 2 = 0) [set pcolor brown]
    if (pycor mod 2 = 1) [set pcolor 7]
  ]

  ;; Diseño del tope de las vías
  ask patches with [ elevacion = 0 and abs(pycor) > 15 and abs(pycor) <= 20 and (pxcor >= 18) and (pxcor <= 20) and (pycor mod 2 = 1)] [set pcolor red]
  ask patches with [ elevacion = 0 and abs(pycor) > 15 and abs(pycor) <= 20 and (pxcor >= 19) and (pxcor <= 20) ] [set pcolor black]

end

;; ------------------------- SET UP DE AGENTES MÓVILES ------------------------

;; Solo existen 2 colectivos, uno por cada sentido
to setup-colectivos
  create-colectivos 2

  ;; Parámetros comunes
  ask colectivos [
    set shape "colectivo"
    set color grey
    set size 12
    set estado 0
    set t_detenido_c 0
  ]

  ;; Parámetros del colectivo inferior
  ask colectivos with [who = 0] [
    set espera_c frecuencia_colectivo / 2   ;; Delay inicial medio
    set heading 90                          ;; Orientación derecha
    set ycor -3                             ;; Carril inferior
  ]

  ;; Parámetros del colectivo superior
  ask colectivos with [who = 1] [
    set espera_c frecuencia_colectivo       ;; Delay inicial completo
    set heading 270                         ;; Orientación izquierda
    set ycor 3                              ;; Carril superior
  ]
end

;; Solo existe 1 tren
to setup-trenes
  create-trenes 1

  ;; Parámetros comunes a cualquier tren
  ask trenes [
    set shape "tren"
    set color grey
    set size 12
    set estado 0
    set t_detenido_t 0
  ]

  ;; Parámetros del tren específico (No hace falta en este caso pero se define así para mayor escalabilidad)
  ask trenes with [who = 2] [
    set espera_t frecuencia_tren           ;; Delay inicial completo
    set heading 90                         ;; Orientación derecha
    set ycor 18                            ;; Vía superior
  ]

end

;; Solo existe 1 subte
to setup-subtes
  create-subtes 1

  ;; Parámetros comunes a cualquier subte
  ask subtes [
    set shape "subte"
    set color grey
    set size 12
    set estado 0
    set t_detenido_s 0
  ]

  ;; Parámetros del subte específico (No hace falta en este caso pero se define así para mayor escalabilidad)
  ask subtes with [who = 3] [
    set espera_s frecuencia_subte         ;; Delay inicial completo
    set heading 90                        ;; Orientación derecha
    set ycor -18                          ;; Vía inferior
  ]

end

;; ------------------------- CREACIÓN DE AGENTES MÓVILES ------------------------
;; ------------------------- CREACIÓN DE PASAJEROS ------------------------------

;; Configuración común para todos los pasajeros
to crear_pasajero_common
  set shape "pasajero"
  set size 2

  ;; establecer edad (aleatoria entre 15 y 85)
  set edad (15 + random 70)
  set label edad

end

;; ------------------------- CREACIÓN DE PASAJEROS PROVENIENTES DEL TREN------------------------------

;; Para evitar choques con los que ingresan al transporte, se crean en "x" par
to crear_pasajero_from_tren
  crear_pasajero_common
  set xcor 16 - 2 * (who mod 5)
  set ycor 15

  if (any? pasajeros-on neighbors) [die]

  set pasajeros_plataforma_superior_tren (pasajeros_plataforma_superior_tren - 1)

end

;; Pasajero que baja del tren a la plataforma superior
to crear_pasajero_superior_tren
  crear_pasajero_from_tren

  set heading 180

  ;; Diferentes variables relacionadas a los lugares libres en las plataformas
  let libres_diferente_plataforma (patches with [plataforma = 1 and libre? = true] )
  let libres_diferente_plataforma_colectivo (patches with [plataforma = 1 and libre? = true and lugar_espera = 1] )
  let libres_diferente_plataforma_subte (patches with [plataforma = 1 and libre? = true and lugar_espera = 2] )
  let libres_misma_plataforma (patches with [plataforma = 0 and libre? = true] )
  let libres_misma_plataforma_colectivo (patches with [plataforma = 0 and libre? = true and lugar_espera = 1] )
  let libres_misma_plataforma_tren (patches with [plataforma = 0 and libre? = true and lugar_espera = 2] )

  ;; Se utiliza un random para decidir si va al subte, uno de los colectivos o abandona la estación
  set transporte_elegido random 4

  let paro_general (paro_colectivo and paro_subte)

  ;; Elección del transporte a utilizar - Se tienen en cuenta los lugares libres para evitar muchos pasajeros en un mismo lugar
  ifelse (count libres_diferente_plataforma = 0 or hora_actual >= 21 or transporte_elegido = 1 or transporte_elegido = 2 or paro_general) [
    ifelse (count libres_misma_plataforma_colectivo = 0 or hora_actual >= 21 or transporte_elegido = 2 or paro_general) [
      ;; Se retira del centro de transbordo
      set estado 15

      ;; Se retiran hacia la derecha
      set calle_plataforma 25

    ]
    [
      ;; Se dirige a tomar el colectivo de la misma plataforma
      set transporte_elegido 1
      set estado 10
      set plataforma_destino 0

      set lugar_elegido one-of libres_misma_plataforma_colectivo
      ask lugar_elegido [set libre? false]
    ]
  ]
  [
    ifelse ((count libres_diferente_plataforma_subte = 0 or transporte_elegido = 0) and count libres_diferente_plataforma_colectivo != 0) [

      ;; Se dirige a tomar el colectivo de la otra plataforma
      set transporte_elegido 0
      set estado 13
      set plataforma_destino 1
      set lugar_elegido one-of libres_diferente_plataforma_colectivo
      ask lugar_elegido [set libre? false]

    ]
    [
      ;; Se dirige a tomar el subte
      set transporte_elegido 3
      set estado 13
      set plataforma_destino 1
      set lugar_elegido one-of libres_diferente_plataforma_subte
      ask lugar_elegido [set libre? false]
    ]
  ]
end

;; ------------------------- CREACIÓN DE PASAJEROS PROVENIENTES DE LOS COLECTIVOS------------------------------

;; Parámetros comunes para ambas plataformas
;; Para evitar choques con los que ingresan a los colectivos, se crean en "x" par
to crear_pasajero_from_colectivo
  crear_pasajero_common
  set xcor 4 - 2 * (who mod 5)

end


;; pasajero que baja del colectivo a la plataforma superior
to crear_pasajero_superior_colectivo
  set ycor 7
  crear_pasajero_from_colectivo

  if (any? pasajeros-on neighbors) [die]

  set pasajeros_plataforma_superior_colectivo (pasajeros_plataforma_superior_colectivo - 1)

  set heading 0

  ;; elección de tipo según celdas libres
  let libres_diferente_plataforma (patches with [plataforma = 1 and libre? = true] )
  let libres_diferente_plataforma_colectivo (patches with [plataforma = 1 and libre? = true and lugar_espera = 1] )
  let libres_diferente_plataforma_subte (patches with [plataforma = 1 and libre? = true and lugar_espera = 2] )
  let libres_misma_plataforma (patches with [plataforma = 0 and libre? = true] )
  let libres_misma_plataforma_colectivo (patches with [plataforma = 0 and libre? = true and lugar_espera = 1] )
  let libres_misma_plataforma_tren (patches with [plataforma = 0 and libre? = true and lugar_espera = 2] )

  ;; Se utiliza un random para decidir si va al tren, subte, el otro colectivo o abandona la estación

  let paro_general (paro_tren and paro_subte)

  ifelse (paro_general)[
    set transporte_elegido random 2
  ][
    set transporte_elegido random 4
  ]

  ifelse (count libres_diferente_plataforma = 0 or hora_actual >= 21 or transporte_elegido = 1 or transporte_elegido = 2) [
    ifelse (count libres_misma_plataforma_tren = 0 or hora_actual >= 21 or transporte_elegido = 1) [
      ;; Se retira del centro de transbordo
      set estado 15

      ;; Se retiran hacia la izquierda
      set calle_plataforma -25

    ]
    [
      ;; Se dirige al tren
      set transporte_elegido 2
      set estado 10
      set plataforma_destino 0
      set lugar_elegido one-of libres_misma_plataforma_tren
      ask lugar_elegido [set libre? false]
    ]
  ]
  [
    ifelse ((count libres_diferente_plataforma_subte = 0 or transporte_elegido = 0) and count libres_diferente_plataforma_colectivo != 0) [

      ;; Se dirige al colectivo de la otra plataforma
      set transporte_elegido 0
      set estado 13
      set plataforma_destino 1
      set lugar_elegido one-of libres_diferente_plataforma_colectivo
      ask lugar_elegido [set libre? false]

    ]
    [
      ;; Se dirige al subte
      set transporte_elegido 3
      set estado 13
      set plataforma_destino 1
      set lugar_elegido one-of libres_diferente_plataforma_subte
      ask lugar_elegido [set libre? false]
    ]
  ]
end

;; Pasajero que baja del colectivo a la plataforma inferior
to crear_pasajero_inferior_colectivo
  set ycor -7
  crear_pasajero_from_colectivo

  if (any? pasajeros-on neighbors) [die]

  set pasajeros_plataforma_inferior_colectivo (pasajeros_plataforma_inferior_colectivo - 1)

  set heading 180

  ;; Elección de tipo según celdas libres
  let libres_diferente_plataforma (patches with [plataforma = 0 and libre? = true] )
  let libres_diferente_plataforma_colectivo (patches with [plataforma = 0 and libre? = true and lugar_espera = 1] )
  let libres_diferente_plataforma_tren (patches with [plataforma = 0 and libre? = true and lugar_espera = 2] )
  let libres_misma_plataforma (patches with [plataforma = 1 and libre? = true] )
  let libres_misma_plataforma_colectivo (patches with [plataforma = 1 and libre? = true and lugar_espera = 1] )
  let libres_misma_plataforma_subte (patches with [plataforma = 1 and libre? = true and lugar_espera = 2] )

  ;; Se utiliza un random para decidir si va al subte, tren, el otro colectivo o abandona la estación


  let paro_general (paro_tren and paro_subte)

  ifelse (paro_general)[
    set transporte_elegido random 2
  ][
    set transporte_elegido random 4
  ]


  ifelse (count libres_diferente_plataforma = 0 or hora_actual >= 21 or transporte_elegido = 0 or transporte_elegido = 3) [
    ifelse (count libres_misma_plataforma_subte = 0 or hora_actual >= 21 or transporte_elegido = 0) [
      ;; Se retira del centro de transbordo
      set estado 15

      ;; Se retiran hacia la izquierda
      set calle_plataforma -25

    ]
    [
      ;; Se dirige al subte
      set transporte_elegido 3
      set estado 10
      set plataforma_destino 1
      set lugar_elegido one-of libres_misma_plataforma_subte
      ask lugar_elegido [set libre? false]
    ]
  ]
  [
    ifelse ((count libres_diferente_plataforma_tren = 0 or transporte_elegido = 1) and count libres_diferente_plataforma_colectivo != 0) [

      ;; Se dirige al colectivo de la otra plataforma
      set transporte_elegido 1
      set estado 13
      set plataforma_destino 0
      set lugar_elegido one-of libres_diferente_plataforma_colectivo
      ask lugar_elegido [set libre? false]

    ]
    [
      ;; Se dirige al tren
      set transporte_elegido 2
      set estado 13
      set plataforma_destino 0
      set lugar_elegido one-of libres_diferente_plataforma_tren
      ask lugar_elegido [set libre? false]
    ]
  ]

end

;; ------------------------- CREACIÓN DE PASAJEROS PROVENIENTES DEL SUBTE ------------------------------

;; Para evitar choques con los que ingresan al subte, se crean en "x" par
to crear_pasajero_from_subte
  crear_pasajero_common
  set xcor 16 - 2 * (who mod 5)
  set ycor -15

  if (any? pasajeros-on neighbors) [die]

  set pasajeros_plataforma_inferior_subte (pasajeros_plataforma_inferior_subte - 1)
end

;; Pasajero que baja del subte a la plataforma inferior
to crear_pasajero_inferior_subte
  crear_pasajero_from_subte

  set heading 0

  ;; Elección de tipo según celdas libres
  let libres_diferente_plataforma (patches with [plataforma = 0 and libre? = true] )
  let libres_diferente_plataforma_colectivo (patches with [plataforma = 0 and libre? = true and lugar_espera = 1] )
  let libres_diferente_plataforma_tren (patches with [plataforma = 0 and libre? = true and lugar_espera = 2] )
  let libres_misma_plataforma (patches with [plataforma = 1 and libre? = true] )
  let libres_misma_plataforma_colectivo (patches with [plataforma = 1 and libre? = true and lugar_espera = 1] )
  let libres_misma_plataforma_subte (patches with [plataforma = 1 and libre? = true and lugar_espera = 2] )

  ;; Se utiliza un random para decidir si va al tren, uno de los colectivos o abandona la estación
  set transporte_elegido random 4

  let paro_general (paro_tren and paro_colectivo)

  ;; Elección del transporte a utilizar

  ifelse (count libres_diferente_plataforma = 0 or hora_actual >= 21 or transporte_elegido = 0 or transporte_elegido = 3 or paro_general) [
    ifelse (count libres_misma_plataforma_colectivo = 0 or hora_actual >= 21 or transporte_elegido = 3 or paro_general) [
      ;; Se retira del centro de transbordo
      set estado 15

      ;; Se retiran hacia la derecha
      set calle_plataforma 25

    ]
    [
      ;; Se dirige al colectivo de la misma plataforma
      set transporte_elegido 0
      set estado 10
      set plataforma_destino 1
      set lugar_elegido one-of libres_misma_plataforma_colectivo
      ask lugar_elegido [set libre? false]
    ]
  ]
  [
    ifelse ((count libres_diferente_plataforma_tren = 0 or transporte_elegido = 1) and count libres_diferente_plataforma_colectivo != 0) [

      ;; Se dirige al colectivo de la otra plataforma
      set transporte_elegido 1
      set estado 13
      set plataforma_destino 0
      set lugar_elegido one-of libres_diferente_plataforma_colectivo
      ask lugar_elegido [set libre? false]

    ]
    [
      ;; Se dirige al tren
      set transporte_elegido 2
      set estado 13
      set plataforma_destino 0
      set lugar_elegido one-of libres_diferente_plataforma_tren
      ask lugar_elegido [set libre? false]
    ]
  ]


end

;; ------------------------- CREACIÓN DE PASAJEROS INGRESANTES A LA PLATAFORMA ------------------------------

;; Pasajero que ingresa por alguno de los 4 molinetes
to crear_pasajero_ingreso
  crear_pasajero_common

  ;; Lugar de entrada randomizado
  ifelse (random 2 = 0) [set xcor max-pxcor set heading 270] [set xcor min-pxcor set heading 90]

  ;; Elección de tipo según celdas libres
  let libres_plataforma_superior (patches with [plataforma = 0 and libre? = true] )
  let libres_plataforma_colectivo_superior (patches with [plataforma = 0 and libre? = true and lugar_espera = 1] )
  let libres_plataforma_tren (patches with [plataforma = 0 and libre? = true and lugar_espera = 2] )
  let libres_plataforma_inferior (patches with [plataforma = 1 and libre? = true] )
  let libres_plataforma_colectivo_inferior (patches with [plataforma = 1 and libre? = true and lugar_espera = 1] )
  let libres_plataforma_subte (patches with [plataforma = 1 and libre? = true and lugar_espera = 2] )

  ;; Se utiliza un random para decidir si va al subte, uno de los colectivos o el tren
  set transporte_elegido random 4

   ;; Si no hay lugar, directamente descartar creación
  if (count libres_plataforma_colectivo_superior = 0 and count libres_plataforma_tren = 0 and count libres_plataforma_colectivo_inferior = 0 and count libres_plataforma_subte = 0) [die]


  ;; Elección del transporte a utilizar
  ifelse (count libres_plataforma_inferior != 0 and (count libres_plataforma_superior = 0 or hora_actual >= 21 or transporte_elegido = 0 or transporte_elegido = 3)) [
    ifelse (count libres_plataforma_subte != 0 and (count libres_plataforma_colectivo_inferior = 0 or hora_actual >= 21 or transporte_elegido = 3)) [

      ;; Se dirige al subte
      set transporte_elegido 3
      set estado 10
      set plataforma_destino 1
      set ycor -11
      set lugar_elegido one-of libres_plataforma_subte

    ]
    [
      ;; Se dirige al colectivo de la plataforma inferior
      set transporte_elegido 0
      set estado 10
      set plataforma_destino 1
      set ycor -11
      set lugar_elegido one-of libres_plataforma_colectivo_inferior
    ]
  ]
  [
    ifelse ((count libres_plataforma_tren = 0 or transporte_elegido = 1) and count libres_plataforma_colectivo_superior != 0) [

      ;; Se dirige al colectivo de la plataforma superior
      set transporte_elegido 1
      set estado 10
      set plataforma_destino 0
      set ycor 11
      set lugar_elegido one-of libres_plataforma_colectivo_superior

    ]
    [
      ;; Se dirige al tren
      set transporte_elegido 2
      set estado 10
      set plataforma_destino 0
      set ycor 11
      set lugar_elegido one-of libres_plataforma_tren
    ]
  ]

  ;; Acciones comunes
  ask lugar_elegido [set libre? false]

end


;; ------------------------- UPDATE DE TRANSPORTES SEGÚN ESTADO ---------------------------

;; ------------------------- UPDATE DE COLECTIVOS SEGÚN ESTADO ---------------------------

;; COLECTIVO - ESTADO 0
to update_colectivo_en_marcha

  ;; Realizar accion del estado (avanzar)
  fd 8 * (abs(xcor) + 1) / 50

  ;; Chequear entradas
  ;; Camino 1 - ¿Se encuentra en el lugar de espera?
  if (abs(xcor) < 0.17) [
    set estado 1                                   ;; Cambiar a estado "detenido"
    set t_detenido_c tolerancia_colectivo_detenido   ;; Asignar tiempo de espera

    ifelse (who = 0)                ;; Indicar cuantos pasajeros deben crearse
      [set pasajeros_plataforma_inferior_colectivo cant_bajan_colectivo]
      [set pasajeros_plataforma_superior_colectivo cant_bajan_colectivo]
  ]

  ;; Camino 2 - ¿Se alejó el colectivo lo suficiente?
  if (abs(xcor) >= max-pxcor) [
    set estado 2                                         ;; Cambiar a estado "lejano"
    set espera_c (frecuencia_colectivo + random 400 - 300)      ;; Asignar tiempo hasta próximo arribo
    set hidden? true                                     ;; Volver invisible al usuario
  ]
end


;; COLECTIVO - ESTADO 1
to update_colectivo_detenido
  ;; Realizar acción del estado (disminuir espera si nadie baja)
  if (who = 0 and pasajeros_plataforma_inferior_colectivo = 0) [set t_detenido_c (t_detenido_c - 8)]
  if (who = 1 and pasajeros_plataforma_superior_colectivo = 0) [set t_detenido_c (t_detenido_c - 8)]

  ;; ¿Se cumplió el tiempo de espera?
  if (t_detenido_c <= 0) [
    set estado 0              ;; Pasar al estado en marcha
    fd 0.18                   ;; Avanzar para evitar bloqueo
  ]
end


;; COLECTIVO - ESTADO 2
to update_colectivo_lejano
  ;; Mientras no se alcance las 22 hs y no haya paro, seguir moviendo los colectivos
  if (hora_actual < 22 and not paro_colectivo) [
    ;; Realizar acción del estado (disminuir espera)
    set espera_c (espera_c - 17)

    ;; ¿Se cumplió el tiempo de espera?
    if (espera_c <= 0) [
      ifelse who = 0 [set xcor min-pxcor] [set xcor max-pxcor]
      set hidden? false
      set estado 0
    ]
  ]


end

;; ------------------------- UPDATE DE SUBTE SEGÚN ESTADO ---------------------------

;; SUBTE - ESTADO 0
to update_subte_llegando
  ;; Realizar accion del estado (avanzar)
  fd 8 * (abs(12 - xcor) + 1) / 50

  ;; Chequear entradas
  ;; ¿Se encuentra en el lugar de espera?
  if ((12 - xcor) < 0.08) [
    set estado 1                                   ;; Cambiar a estado "detenido"
    set t_detenido_s tolerancia_subte_detenido   ;; Asignar tiempo de espera

    set pasajeros_plataforma_inferior_subte cant_bajan_subte
  ]

end


;; SUBTE - ESTADO 1
to update_subte_detenido

  ;; Realizar acción del estado (disminuir espera si nadie baja)
  if (pasajeros_plataforma_inferior_subte = 0) [set t_detenido_s (t_detenido_s - 8)]

  ;; ¿Se cumplió el tiempo de espera?
  if (t_detenido_s <= 0) [
    set estado 3              ;; Pasar al estado alejando
    fd -0.16                  ;; Avanzar para evitar bloqueo
  ]
end


;; SUBTE - ESTADO 2
to update_subte_lejano

  ;; Mientras no se alcance las 22 hs y no haya paro, seguir moviendo el subte
  if (hora_actual < 22 and not paro_subte) [
    ;; Realizar acción del estado (disminuir espera)
    set espera_s (espera_s - 17)

    ;; ¿Se cumplió el tiempo de espera?
    if (espera_s <= 0) [
      set xcor min-pxcor
      set hidden? false
      set estado 0
    ]
  ]

end


;; SUBTE - ESTADO 3
to update_subte_alejando
  ;; Realizar accion del estado (avanzar)
  fd -8 * ((12 - xcor) + 1) / 50

  ;; Chequear entradas
  ;; ¿Se alejó el subte lo suficiente?
  if (abs(xcor) >= 18) [
    set estado 2                                         ;; Cambiar a estado "lejano"
    set espera_s (frecuencia_subte + random 400 - 300)   ;; Asignar tiempo hasta próximo arribo
    set hidden? true                                     ;; Volver invisible al usuario
  ]

end

;; ------------------------- UPDATE DE TREN SEGÚN ESTADO ---------------------------

;; TREN - ESTADO 0
to update_tren_llegando
  ;; Realizar accion del estado (avanzar)
  fd 8 * (abs(12 - xcor) + 1) / 50

  ;; Chequear entradas
  ;; ¿Se encuentra en el lugar de espera?
  if ((12 - xcor) < 0.08) [
    set estado 1                                   ;; Cambiar a estado "detenido"
    set t_detenido_t tolerancia_tren_detenido   ;; Asignar tiempo de espera

    set pasajeros_plataforma_superior_tren cant_bajan_tren

  ]

end


;; TREN - ESTADO 1
to update_tren_detenido

  ;; Realizar acción del estado (disminuir espera si nadie baja)
  if (pasajeros_plataforma_superior_tren = 0) [set t_detenido_t (t_detenido_t - 8)]

  ;; ¿Se cumplió el tiempo de espera?
  if (t_detenido_t <= 0) [
    set estado 3              ;; Pasar al estado alejando
    fd -0.16                   ;; Avanzar para evitar bloqueo
  ]
end


;; TREN - ESTADO 2
to update_tren_lejano
  ;; Mientras no se alcance las 22 hs y no haya paro, seguir moviendo el tren
  if (hora_actual < 22 and not paro_tren ) [
    ;; Realizar acción del estado (disminuir espera)
    set espera_t (espera_t - 17)

    ;; ¿Se cumplió el tiempo de espera?
    if (espera_t <= 0) [
      set xcor min-pxcor
      set hidden? false
      set estado 0
    ]
  ]


end

;; TREN - ESTADO 3
to update_tren_alejando
  ;; Realizar accion del estado (avanzar)
  fd -8 * ((12 - xcor) + 1) / 50

  ;; Chequear entradas
  ;; ¿Se alejó el colectivo lo suficiente?
  if (abs(xcor) >= 18) [
    set estado 2                                         ;; Cambiar a estado "lejano"
    set espera_t (frecuencia_tren + random 400 - 300)    ;; Asignar tiempo hasta próximo arribo
    set hidden? true                                     ;; Volver invisible al usuario
  ]

end

;; ------------------------- UPDATE DE PASAJEROS SEGÚN ESTADO ---------------------------

;; PASAJERO - ESTADO 10
to update_pasajero_hacia_parada
  ;; Dirigirse hacia el lugar_elegido
  let x_esperada [pxcor] of lugar_elegido
  let y_esperada [pycor] of lugar_elegido

  set color orange                 ;; color naranja

  ;; Avanzar hacia el lugar elegido
  facexy x_esperada y_esperada

  ;; Avanzar solo si no hay nadie delante
  avanzar_si_hay_espacio

  ;; Llego al lugar esperado?
  if (pycor = y_esperada and pxcor = x_esperada) [

    set estado 11                 ;; Cambiar a estado esperando
    set tiempo_espera 2000         ;; Tiempo que espera cada pasajero a que llegue el transporte
    ifelse (transporte_elegido = 1 or transporte_elegido = 3)
      [set heading 180]
      [set heading 0]
  ]
end

;; PASAJERO - ESTADO 11
;; Para evitar choques con los que bajan, esperan en "y" impar
to update_pasajero_esperando
  set tiempo_espera (tiempo_espera - 17)

  set color red                 ;; color rojo
  ;; Cambiar a estado "subiendo a transporte" si llegó
  let paro_general (paro_tren and paro_subte and paro_colectivo)

  ifelse (transporte_elegido = 0 or transporte_elegido = 1)
  [
    ifelse (transporte_elegido = 0)
    [
      ifelse (paro_general) [
            ;; Se retira del centro de transbordo
            set estado 15

            ;; Se retiran hacia la izquierda
            set calle_plataforma -25
      ][
        if paro_colectivo [set tiempo_espera 0]
      ]
      if patch-here = lugar_elegido and any? colectivos with [who = 0 and estado = 1] [set estado 12 set color pink ]
    ]
    [
      ifelse (paro_general) [
            ;; Se retira del centro de transbordo
            set estado 15

            ;; Se retiran hacia la izquierda
            set calle_plataforma -25
      ][
        if paro_colectivo [set tiempo_espera 0]
      ]
      if patch-here = lugar_elegido and any? colectivos with [who = 1 and estado = 1] [set estado 12 set color pink ]
    ]
  ]
  [
    ifelse (transporte_elegido = 3)
    [
       ifelse (paro_general) [
            ;; Se retira del centro de transbordo
            set estado 15

            ;; Se retiran hacia la derecha
            set calle_plataforma 25
      ][
        if paro_subte [set tiempo_espera 0]
      ]
      if patch-here = lugar_elegido and any? subtes with [who = 3 and estado = 1] [set estado 12 set color pink ]
    ]
    [
      ifelse (paro_general) [
            ;; Se retira del centro de transbordo
            set estado 15

            ;; Se retiran hacia la derecha
            set calle_plataforma 25
      ][
        if (paro_tren) [set tiempo_espera 0]
      ]
      if patch-here = lugar_elegido and any? trenes with [who = 2 and estado = 1] [set estado 12 set color pink ]
    ]
  ]

  ;; ¿Se cumplió el tiempo de espera? -> Suponer que hubo un paro y cambiar de transporte - Utiliza la misma idea que la asignación inicial
  ;; pero además incluye un porcentaje relacionado a la similitud de recorridos y la liberación del espacio previamente elegido
  if (tiempo_espera <= 0) [
    ifelse (transporte_elegido = 0 or transporte_elegido = 1)
    [
      ifelse (transporte_elegido = 0)
      [

        let libres_diferente_plataforma (patches with [plataforma = 0 and libre? = true] )
        let libres_diferente_plataforma_colectivo (patches with [plataforma = 0 and libre? = true and lugar_espera = 1] )
        let libres_diferente_plataforma_tren (patches with [plataforma = 0 and libre? = true and lugar_espera = 2] )
        let libres_misma_plataforma (patches with [plataforma = 1 and libre? = true] )
        let libres_misma_plataforma_colectivo (patches with [plataforma = 1 and libre? = true and lugar_espera = 1] )
        let libres_misma_plataforma_subte (patches with [plataforma = 1 and libre? = true and lugar_espera = 2] )

        ;; En base al porcentaje
        let probabilidad random 100
        ifelse (probabilidad < 50) [
          ifelse (probabilidad < 15)[
            ;; El 15% de las veces el pasajero se retira
            set transporte_elegido 0
          ][
            ;; El 35% de las veces el pasajero se dirige al otro colectivo
            set transporte_elegido 1
          ]
        ][
          ifelse (probabilidad < 83)[
            ;; El 33% de las veces el pasajero se dirige al subte
            set transporte_elegido 3
          ][
            ;; El 17% de las veces el pasajero se dirige al tren
            set transporte_elegido 2
          ]
        ]


        ;; Elección contemplando la falta de lugares libres

        ifelse (count libres_diferente_plataforma = 0 or hora_actual >= 21 or transporte_elegido = 0 or transporte_elegido = 3) [
          ifelse (count libres_misma_plataforma_subte = 0 or hora_actual >= 21 or transporte_elegido = 0) [
            ;; Se retira del centro de transbordo
            set estado 15

            ;; Se retiran hacia la izquierda
            set calle_plataforma -25

          ]
          [
            set transporte_elegido 3
            set estado 10
            set plataforma_destino 1
            if lugar_elegido != 0 [
              ask lugar_elegido [set libre? true]
            ]
            set lugar_elegido one-of libres_misma_plataforma_subte
            ask lugar_elegido [set libre? false]
          ]
        ]
        [
          ifelse ((count libres_diferente_plataforma_tren = 0 or transporte_elegido = 1) and count libres_diferente_plataforma_colectivo != 0) [

            set transporte_elegido 1
            set estado 13
            set plataforma_destino 0
            if lugar_elegido != 0 [
              ask lugar_elegido [set libre? true]
            ]
            set lugar_elegido one-of libres_diferente_plataforma_colectivo
            ask lugar_elegido [set libre? false]
          ]
          [
            set transporte_elegido 2
            set estado 13
            set plataforma_destino 0
            if lugar_elegido != 0 [
              ask lugar_elegido [set libre? true]
            ]
            set lugar_elegido one-of libres_diferente_plataforma_tren
            ask lugar_elegido [set libre? false]
          ]
        ]
      ]
      [
        ;; elección de tipo según celdas libres
        let libres_diferente_plataforma (patches with [plataforma = 1 and libre? = true] )
        let libres_diferente_plataforma_colectivo (patches with [plataforma = 1 and libre? = true and lugar_espera = 1] )
        let libres_diferente_plataforma_subte (patches with [plataforma = 1 and libre? = true and lugar_espera = 2] )
        let libres_misma_plataforma (patches with [plataforma = 0 and libre? = true] )
        let libres_misma_plataforma_colectivo (patches with [plataforma = 0 and libre? = true and lugar_espera = 1] )
        let libres_misma_plataforma_tren (patches with [plataforma = 0 and libre? = true and lugar_espera = 2] )

        ;; En base al porcentaje
        let probabilidad random 100
        ifelse (probabilidad < 50) [
          ifelse (probabilidad < 15)[
            ;; El 15% de las veces el pasajero se retira
            set transporte_elegido 1
          ][
            ;; El 35% de las veces el pasajero se dirige al otro colectivo
            set transporte_elegido 0
          ]
        ][
          ifelse (probabilidad < 83)[
            ;; El 33% de las veces el pasajero se dirige al subte
            set transporte_elegido 3
          ][
            ;; El 17% de las veces el pasajero se dirige al tren
            set transporte_elegido 2
          ]
        ]

        ;; elección del transporte a utilizar

        ifelse (count libres_diferente_plataforma = 0 or hora_actual >= 21 or transporte_elegido = 1 or transporte_elegido = 2) [
          ifelse (count libres_misma_plataforma_tren = 0 or hora_actual >= 21 or transporte_elegido = 1) [
            ;; Se retira del centro de transbordo
            set estado 15

            ;; Se retiran hacia la izquierda
            set calle_plataforma -25

          ]
          [
            set transporte_elegido 2
            set estado 10
            set plataforma_destino 0
            if lugar_elegido != 0 [
              ask lugar_elegido [set libre? true]
            ]
            set lugar_elegido one-of libres_misma_plataforma_tren
            ask lugar_elegido [set libre? false]
          ]
        ]
        [
          ifelse ((count libres_diferente_plataforma_subte = 0 or transporte_elegido = 0) and count libres_diferente_plataforma_colectivo != 0) [

            set transporte_elegido 0
            set estado 13
            set plataforma_destino 1
            if lugar_elegido != 0 [
              ask lugar_elegido [set libre? true]
            ]
            set lugar_elegido one-of libres_diferente_plataforma_colectivo
            ask lugar_elegido [set libre? false]

          ]
          [
            set transporte_elegido 3
            set estado 13
            set plataforma_destino 1
            if lugar_elegido != 0 [
              ask lugar_elegido [set libre? true]
            ]
            set lugar_elegido one-of libres_diferente_plataforma_subte
            ask lugar_elegido [set libre? false]
          ]
        ]
      ]
    ]
    [
      ifelse (transporte_elegido = 3)
      [
        ;; Elección de tipo según celdas libres
        let libres_diferente_plataforma (patches with [plataforma = 0 and libre? = true] )
        let libres_diferente_plataforma_colectivo (patches with [plataforma = 0 and libre? = true and lugar_espera = 1] )
        let libres_diferente_plataforma_tren (patches with [plataforma = 0 and libre? = true and lugar_espera = 2] )
        let libres_misma_plataforma (patches with [plataforma = 1 and libre? = true] )
        let libres_misma_plataforma_colectivo (patches with [plataforma = 1 and libre? = true and lugar_espera = 1] )
        let libres_misma_plataforma_subte (patches with [plataforma = 1 and libre? = true and lugar_espera = 2] )

        ;; En base al porcentaje
        let probabilidad random 100
        ifelse (probabilidad < 60) [
          ifelse (probabilidad < 30)[
            ;; El 30% de las veces el pasajero se dirige a un colectivo
            set transporte_elegido 0
          ][
            ;; El 30% de las veces el pasajero se dirige al otro colectivo
            set transporte_elegido 1
          ]
        ][
          ifelse (probabilidad < 96)[
            ;; El 36% de las veces el pasajero se retira
            set transporte_elegido 3
          ][
            ;; El 4% de las veces el pasajero se dirige al tren debido a la poca similitud de los recorridos
            set transporte_elegido 2
          ]
        ]

        ;; Elección del transporte a utilizar

        ifelse (count libres_diferente_plataforma = 0 or hora_actual >= 21 or transporte_elegido = 0 or transporte_elegido = 3) [
          ifelse (count libres_misma_plataforma_colectivo = 0 or hora_actual >= 21 or transporte_elegido = 3) [
            ;; Se retira del centro de transbordo
            set estado 15

            ;; Se retiran hacia la derecha
            set calle_plataforma 25

          ]
          [
            set transporte_elegido 0
            set estado 10
            set plataforma_destino 1
            if lugar_elegido != 0 [
              ask lugar_elegido [set libre? true]
            ]
            set lugar_elegido one-of libres_misma_plataforma_colectivo
            ask lugar_elegido [set libre? false]
          ]
        ]
        [
          ifelse ((count libres_diferente_plataforma_tren = 0 or transporte_elegido = 1) and count libres_diferente_plataforma_colectivo != 0) [

            set transporte_elegido 1
            set estado 13
            set plataforma_destino 0
            if lugar_elegido != 0 [
              ask lugar_elegido [set libre? true]
            ]
            set lugar_elegido one-of libres_diferente_plataforma_colectivo
            ask lugar_elegido [set libre? false]

          ]
          [
            set transporte_elegido 2
            set estado 13
            set plataforma_destino 0
            if lugar_elegido != 0 [
              ask lugar_elegido [set libre? true]
            ]
            set lugar_elegido one-of libres_diferente_plataforma_tren
            ask lugar_elegido [set libre? false]
          ]
        ]
      ]
      [
        ;; Diferentes variables relacionadas a los lugares libres en las plataformas
        let libres_diferente_plataforma (patches with [plataforma = 1 and libre? = true] )
        let libres_diferente_plataforma_colectivo (patches with [plataforma = 1 and libre? = true and lugar_espera = 1] )
        let libres_diferente_plataforma_subte (patches with [plataforma = 1 and libre? = true and lugar_espera = 2] )
        let libres_misma_plataforma (patches with [plataforma = 0 and libre? = true] )
        let libres_misma_plataforma_colectivo (patches with [plataforma = 0 and libre? = true and lugar_espera = 1] )
        let libres_misma_plataforma_tren (patches with [plataforma = 0 and libre? = true and lugar_espera = 2] )

        ;; En base al porcentaje
        let probabilidad random 100
        ifelse (probabilidad < 60) [
          ifelse (probabilidad < 30)[
            ;; El 30% de las veces el pasajero se dirige a un colectivo
            set transporte_elegido 0
          ][
            ;; El 30% de las veces el pasajero se dirige al otro colectivo
            set transporte_elegido 1
          ]
        ][
          ifelse (probabilidad < 96)[
            ;; El 36% de las veces el pasajero se retira
            set transporte_elegido 2
          ][
            ;; El 4% de las veces el pasajero se dirige al subte debido a la poca similitud de los recorridos
            set transporte_elegido 3
          ]
        ]

        ;; Elección del transporte a utilizar
        ifelse (count libres_diferente_plataforma = 0 or hora_actual >= 21 or transporte_elegido = 1 or transporte_elegido = 2) [
          ifelse (count libres_misma_plataforma_colectivo = 0 or hora_actual >= 21 or transporte_elegido = 2) [
            ;; Se retira del centro de transbordo
            set estado 15

            ;; Se retiran hacia la derecha
            set calle_plataforma 25

          ]
          [
            set transporte_elegido 1
            set estado 10
            set plataforma_destino 0
            if lugar_elegido != 0 [
              ask lugar_elegido [set libre? true]
            ]
            set lugar_elegido one-of libres_misma_plataforma_colectivo
            ask lugar_elegido [set libre? false]
          ]
        ]
        [
          ifelse ((count libres_diferente_plataforma_subte = 0 or transporte_elegido = 0) and count libres_diferente_plataforma_colectivo != 0) [

            set transporte_elegido 0
            set estado 13
            set plataforma_destino 1
            if lugar_elegido != 0 [
              ask lugar_elegido [set libre? true]
            ]
            set lugar_elegido one-of libres_diferente_plataforma_colectivo
            ask lugar_elegido [set libre? false]

          ]
          [
            set transporte_elegido 3
            set estado 13
            set plataforma_destino 1
            if lugar_elegido != 0 [
              ask lugar_elegido [set libre? true]
            ]
            set lugar_elegido one-of libres_diferente_plataforma_subte
            ask lugar_elegido [set libre? false]
          ]
        ]
      ]
    ]
  ]
end


;; PASAJERO - ESTADO 12
;; No se acercarán al tren hasta que terminen de bajar
;; Para evitar choques con los que bajan, se sube por "y" impar
to update_pasajero_subiendo
  ;; Realizar acción del estado (acercarse a tren)
  let y_subida 0
  set color white                 ;; color amarillo en contraste al puente

  ifelse (transporte_elegido = 0 or transporte_elegido = 1)
  [
    set y_subida 6 * (- plataforma_destino * 2 + 1)
  ]
  [
    set y_subida 15 * (- plataforma_destino * 2 + 1)
  ]
  let habilitado 0

  if (transporte_elegido = 2 and pasajeros_plataforma_superior_tren = 0 and any? trenes with [who = 2 and estado = 1]) [set habilitado 1]
  if (transporte_elegido = 1 and pasajeros_plataforma_superior_colectivo = 0 and any? colectivos with [who = 1 and estado = 1]) [set habilitado 1]
  if (transporte_elegido = 0 and pasajeros_plataforma_inferior_colectivo = 0 and any? colectivos with [who = 0 and estado = 1]) [set habilitado 1]
  if (transporte_elegido = 3 and pasajeros_plataforma_inferior_subte = 0 and any? subtes with [who = 3 and estado = 1]) [set habilitado 1]

  if (habilitado = 1 and pycor != y_subida) [
     ifelse (transporte_elegido = 0 or transporte_elegido = 1)
    [
      facexy 0 y_subida
    ]
    [
      facexy 12 y_subida
    ]
    avanzar_si_hay_espacio
  ]

  ;; Cambiar a estado "hacia_parada" si se fue el transporte

  ifelse (transporte_elegido = 0 or transporte_elegido = 1)
  [
    ifelse (transporte_elegido = 0)
    [
      if any? colectivos with [who = 0 and estado != 1] [set estado 10 ]
    ]
    [
      if any? colectivos with [who = 1 and estado != 1] [set estado 10]
    ]
  ]
  [
    ifelse (transporte_elegido = 3)
    [
      if any? subtes with [who = 3 and estado != 1] [set estado 10 ]
    ]
    [
      if any? trenes with [who = 2 and estado != 1] [set estado 10 ]
    ]
  ]

  ;; Eliminar agente si ya se encuentra en el tren
  if (abs(ycor) <= 6.8 or abs(ycor) >= 14.2) [eliminar_pasajero]
end


;; PASAJERO - ESTADO 13
;; Dirigiendose a la senda
to update_pasajero_hacia_senda
  ;; Realizar acción del estado (acercarse a la senda)
  let signo (ycor / abs(ycor))
  let y_camino (8 * signo)
  let y_esperada y_camino
  let x_puente 11 + plataforma_destino mod 2

  set color brown            ;; color amarillo

  ;; ordenado según distancia al puente
  facexy x_puente y_esperada

  ;; avanzar solo si no hay nadie delante
  avanzar_si_hay_espacio

  ;; Camino 1 - ¿Llego a la senda?
  if (pxcor = x_puente and pycor = y_esperada) [
    set estado 14                 ;; cambiar a estado "cruzando"
    ifelse (plataforma_destino = 1)
      [set heading 180]
      [set heading 0]
  ]
end

;; PASAJERO - ESTADO 14
to update_pasajero_cruzando
  ;; Realizar acción del estado (ir hacia plataforma destino)
  let y_final (- 2 * (plataforma_destino mod 2) + 1) * 10
  let x_puente 11 + plataforma_destino mod 2
  facexy x_puente y_final

  set color yellow

  ;; avanzar solo si no hay nadie delante
  avanzar_si_hay_espacio

  ;; Si llegó a la plataforma destino...
  if (abs(ycor) > 9 ) [
    set estado 10
  ]
end


;; PASAJERO - ESTADO 15
to update_pasajero_hacia_salida
  ;; Realizar acción del estado (acercarse a la salida)
  let signo (- plataforma * 2 + 1)
  let y_esperada (12 + who mod 2) * signo

  set color pink                 ;; color amarillo en contraste al puente

  ifelse (pycor != y_esperada)
    [facexy xcor y_esperada]
    [facexy calle_plataforma ycor]

  ;; avanzar solo si no hay nadie delante
  avanzar_si_hay_espacio

  ;; Camino 1 - ¿Llegó a la salida?
  if (abs(xcor) >= 19.5) [eliminar_pasajero]
end


;; ------------------------------------------ RESPUESTAS A BOTONES ----------------------------

to setup
  clear-all
  setup-patches
  ;; Genera los colectivos
  setup-colectivos
  ;; Genera el tren
  setup-trenes
  ;; Genera el subte
  setup-subtes
  reset-ticks

  set hora_actual hora_inicio

  ;; establecer cantidad que bajan según mes
  cant_bajan_segun_mes

end

;; para cada tick del sistema
to go

  let t (ticks mod 17)

  (ifelse
    ;; actualización de trenes según su estado
    t = 0 [ ask colectivos with [estado = 0] [update_colectivo_en_marcha] ]
    t = 1 [ ask colectivos with [estado = 1] [update_colectivo_detenido] ]
    t = 2 [ ask colectivos with [estado = 2] [update_colectivo_lejano] ]
    ;; actualización de subtes según su estado
    t = 3 [ ask subtes with [estado = 0] [update_subte_llegando] ]
    t = 4 [ ask subtes with [estado = 1] [update_subte_detenido] ]
    t = 5 [ ask subtes with [estado = 2] [update_subte_lejano] ]
    t = 6 [ ask subtes with [estado = 3] [update_subte_alejando] ]
    ;; actualización de trenes según su estado
    t = 7 [ ask trenes with [estado = 0] [update_tren_llegando] ]
    t = 8 [ ask trenes with [estado = 1] [update_tren_detenido] ]
    t = 9 [ ask trenes with [estado = 2] [update_tren_lejano] ]
    t = 10 [ ask trenes with [estado = 3] [update_tren_alejando] ]
    ;; actualización de pasajeros según su estado
    t = 11 [ ask pasajeros with [estado = 10] [update_pasajero_hacia_parada] ]
    t = 12 [ ask pasajeros with [estado = 11] [update_pasajero_esperando] ]
    t = 13 [ ask pasajeros with [estado = 12] [update_pasajero_subiendo] ]
    t = 14 [ ask pasajeros with [estado = 13] [update_pasajero_hacia_senda] ]
    t = 15 [ ask pasajeros with [estado = 14] [update_pasajero_cruzando] ]
    t = 16 [ ask pasajeros with [estado = 15] [update_pasajero_hacia_salida] ]
    )


  ;; se crean cada 5 ticks = 5/100 min = 0.05 min = 3 segundos
  if (ticks mod 5 = 0 and pasajeros_plataforma_superior_tren > 0) [ create-pasajeros 1 [crear_pasajero_superior_tren] ]
  if (ticks mod 5 = 1 and pasajeros_plataforma_superior_colectivo > 0) [ create-pasajeros 1 [crear_pasajero_superior_colectivo] ]
  if (ticks mod 5 = 2 and pasajeros_plataforma_inferior_subte > 0) [ create-pasajeros 1 [crear_pasajero_inferior_subte] ]
  if (ticks mod 5 = 3 and pasajeros_plataforma_inferior_colectivo > 0) [ create-pasajeros 1 [crear_pasajero_inferior_colectivo] ]

  ;; generación espontánea de pasajeros (desde molinetes)
  ;; se crean cada 100 ticks = 1 minuto
  if (ticks mod 100 = 99 and ingresantes = true and hora_actual < 21) [create-pasajeros 1 [crear_pasajero_ingreso] ]

  ;; actualizar frecuencia (solo 1 vez por hora)
  if (ticks mod 6000 = 1) [
    set hora_actual floor(ticks / 6000) + hora_inicio

    if (autocorregir) [autocorregir_valores]

    ;; si son las 11 PM, terminar simulación
    if (hora_actual >= 24) [stop]
  ]

  ;; incrementar ticks
  tick
end


;; --------------------------------------------- FUNCIONES AUXILIARES ---------------------------------

to avanzar_si_hay_espacio
  if (patch-ahead 1 = nobody or count pasajeros-on patch-ahead 1 < 3) [
    avanzar_segun_edad
  ]
end

to avanzar_segun_edad
  ifelse (edad < 50) [fd 0.8] [fd 0.4]
end

to eliminar_pasajero
  if lugar_elegido != 0 [
    ask lugar_elegido [set libre? true]
  ]

  die
end

to autocorregir_valores
  ;; Corregir cantidad que bajan y frecuencias según mes y sea hora pico o no
  cant_bajan_segun_mes

  set frecuencia_subte 600 ;; cada 6 min

  (ifelse
    hora_actual = 5 [set frecuencia_tren 1200]      ;; cada 12 min
    hora_actual = 12 [set frecuencia_tren 1500]     ;; cada 15 min
    hora_actual = 17 [set frecuencia_tren 1200]     ;; cada 12 min
    hora_actual = 21 [set frecuencia_tren 1500]     ;; cada 15 min
  )

  (ifelse
    hora_actual = 5 [set frecuencia_colectivo 1647]      ;; cada 16 min
    hora_actual = 6 [set frecuencia_colectivo 1173]      ;; cada 11 min
    hora_actual = 7 [set frecuencia_colectivo 1088]      ;; cada 10 min
    hora_actual = 8 [set frecuencia_colectivo 1082]      ;; cada 10 min
    hora_actual = 9 [set frecuencia_colectivo 1147]      ;; cada 11 min
    hora_actual = 10 [set frecuencia_colectivo 1147]      ;; cada 11 min
    hora_actual = 11 [set frecuencia_colectivo 1147]      ;; cada 11 min
    hora_actual = 12 [set frecuencia_colectivo 1147]      ;; cada 11 min
    hora_actual = 13 [set frecuencia_colectivo 1135]      ;; cada 11 min
    hora_actual = 14 [set frecuencia_colectivo 1179]      ;; cada 11 min
    hora_actual = 15 [set frecuencia_colectivo 1173]      ;; cada 11 min
    hora_actual = 16 [set frecuencia_colectivo 1147]      ;; cada 11 min
    hora_actual = 17 [set frecuencia_colectivo 1114]      ;; cada 11 min
    hora_actual = 18 [set frecuencia_colectivo 1132]      ;; cada 11 min
    hora_actual = 19 [set frecuencia_colectivo 1367]      ;; cada 13 min
    hora_actual = 20 [set frecuencia_colectivo 1476]      ;; cada 14 min
    hora_actual = 21 [set frecuencia_colectivo 1682]      ;; cada 16 min
  )

end


;; ------------------------------------------ CONSULTA DE DATOS REALES ------------------------------

to cant_bajan_segun_mes
  let total_colectivo 0
  let total_tren 0
  let total_subte 0

  (ifelse
    mes = "Enero"      [set total_colectivo 13574190
                        set total_tren 2080840
                        set total_subte 1573597]
    mes = "Febrero"    [set total_colectivo 15150269
                        set total_tren 2547088
                        set total_subte 2007080]
    mes = "Marzo"      [set total_colectivo 19787506
                        set total_tren 3116183
                        set total_subte 2734983]
    mes = "Abril"      [set total_colectivo 20266426
                        set total_tren 3311077
                        set total_subte 2895216]
    mes = "Mayo"       [set total_colectivo 20853000
                        set total_tren 3518753
                        set total_subte 2980462]
    mes = "Junio"      [set total_colectivo 20485798
                        set total_tren 3373757
                        set total_subte 3053217]
    mes = "Julio"      [set total_colectivo 20274906
                        set total_tren 3603784
                        set total_subte 3076130]
    mes = "Agosto"     [set total_colectivo 21629671
                        set total_tren 3419443
                        set total_subte 3208249]
    mes = "Septiembre" [set total_colectivo 21648922
                        set total_tren 3673601
                        set total_subte 3363122]
    mes = "Octubre"    [set total_colectivo 21434688
                        set total_tren 3284024
                        set total_subte 3198533]
    mes = "Noviembre"  [set total_colectivo 22168217
                        set total_tren 3101962
                        set total_subte 3212892]
    mes = "Diciembre"  [set total_colectivo 19605790
                        set total_tren 2686102
                        set total_subte 2863642]
  )

  let hora_pico (hora_actual < 12 or (hora_actual >= 17 and hora_actual < 21))

  ;; Cálculo de la hora pico para trenes: de los 80 trenes que pasan, 55 de ellos pasan en hora pico (casi un 70%)
  ;; suponemos que en hora pico viaja el doble de pasajeros que en hora no pico
  ;; resulta entonces que el 80% de los pasajeros viaja en hora pico

  ifelse (hora_pico)
    [set cant_bajan_tren round(total_tren * 0.8 / (30 * 110 * 10))]
    [set cant_bajan_tren round(total_tren * 0.2 / (30 * 50 * 10))]

  ;; La frecuencia del subte es constante aunque se estima que viaja el doble de gente en hora pico
  ;; que en no pico por lo que:

  ifelse (hora_pico)
    [set cant_bajan_subte round(total_subte * 0.66 / (30 * 340 * 10))]
    [set cant_bajan_subte round(total_subte * 0.33 / (30 * 340 * 10))]

  ;; Cálculo de la hora pico para trenes: de los 1800 colectivos que pasan, 1260 de ellos pasan en hora pico (más del 70%)
  ;; suponemos que en hora pico viaja el doble de pasajeros que en hora no pico
  ;; resulta entonces que el 80% de los pasajeros viaja en hora pico

  ifelse (hora_pico)
    [set cant_bajan_colectivo round(total_colectivo * 0.8 / (30 * 252 * 10 * 10))]
    [set cant_bajan_colectivo round(total_colectivo * 0.2 / (30 * 108 * 10 * 10))]

end


;; --------------------------------------------- MONITORES -------------------------------------------

to-report mostrar-hora-actual
  let minutos floor((ticks mod 6000) / 100)

  ifelse (minutos < 10)
    [report (word hora_actual ":0" minutos)]
  [report (word hora_actual ":" minutos)]
end
